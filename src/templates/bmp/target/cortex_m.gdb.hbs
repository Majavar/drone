set $TPIU_ACPR = 0xE0040010

set $TPIU_SPPR = 0xE00400F0
set $TPIU_SPPR_NRZ = 0b10

set $TPIU_FFCR = 0xE0040304
set $TPIU_FFCR_ENFCONT = 0b10

set $ITM_LAR = 0xE0000FB0
set $ITM_LAR_CODE = 0xC5ACCE55

set $ITM_TCR = 0xE0000E80
set $ITM_TCR_TRACE_BUS_ID_OFFSET = 16
set $ITM_TCR_TRACE_BUS_ID_MASK = 0b11111110000000000000000
set $ITM_TCR_GTSFREQ_MASK      = 0b00000000000110000000000
set $ITM_TCR_TSPRESCALE_MASK   = 0b00000000000001100000000
set $ITM_TCR_SWOENA            = 0b00000000000000000010000
set $ITM_TCR_TXENA             = 0b00000000000000000001000
set $ITM_TCR_SYNCENA           = 0b00000000000000000000100
set $ITM_TCR_TSENA             = 0b00000000000000000000010
set $ITM_TCR_ITMENA            = 0b00000000000000000000001

set $ITM_TPR = 0xE0000E40
set $ITM_TER0 = 0xE0000E00

set $DWT_CTRL = 0xE0001000
set $DWT_CTRL_SYNCTAP_26   = 0b100000000000
set $DWT_CTRL_SYNCTAP_MASK = 0b110000000000
set $DWT_CTRL_CYCCNTENA    = 0b000000000001

set $DWT_CYCCNT = 0xE0001004

set $RESET_VECTOR = 0x00000004

define drone_break_reset_cortex_m
    set $RESET_HANDLER = {int}$RESET_VECTOR
    thbreak *$RESET_HANDLER
    run
end

define drone_itm_cortex_m
    set {int}$TPIU_ACPR = $INITIAL_ITM_FREQ / {{config.bmp.uart_baudrate}} - 1
    set {int}$TPIU_SPPR = $TPIU_SPPR_NRZ
    set {int}$TPIU_FFCR = {int}$TPIU_FFCR & ~$TPIU_FFCR_ENFCONT
    set {int}$ITM_LAR = $ITM_LAR_CODE
    set {int}$ITM_TCR = ({int}$ITM_TCR & ~( \
            $ITM_TCR_TSENA | $ITM_TCR_TXENA | $ITM_TCR_SWOENA | $ITM_TCR_TSPRESCALE_MASK | \
            $ITM_TCR_GTSFREQ_MASK | $ITM_TCR_TRACE_BUS_ID_MASK \
        )) | $ITM_TCR_SYNCENA | $ITM_TCR_ITMENA | (1 << $ITM_TCR_TRACE_BUS_ID_OFFSET)
    set {int}$ITM_TPR = 0
    set {int}$ITM_TER0 = 0{{#each ports}} | (1 << {{this}}){{/each}}
    set {int}$DWT_CTRL = ({int}$DWT_CTRL & ~$DWT_CTRL_SYNCTAP_MASK) | $DWT_CTRL_CYCCNTENA | $DWT_CTRL_SYNCTAP_26
    set {int}$DWT_CYCCNT = 0xFFFFFFFF
end
